# Язык описания EBNF-грамматик

## Структура файла

Файл с описанем грамматики состоит из трёх частей - имени стартого нетерминала, описания имен "псевдонимов" 
и набора правил. Каждый раздел начинается с его имени (`start`, `names`, `rules`) и двоеточия. 
Дальше следует описание раздела - строчки наинающиеся с 2х пробелов. Все пробельные символы кроме этих, 
и тех, что внутри лексем нетерминалов и терминалов игнорируются.

Таким образом файл выглядит следующим образом

```yaml
start:
  ....
names:
  ....
  ....
rules:
  ....
  .... 
```

## Лексемы языка 

### Нетерминалы

Нетерминалы представляется как его имя в угловых скобочках, при этом разрешено экранирование символов
`>` и `\`. Имя нетерминала - произвольный набор ASCII - символов

### Имена

Имена записываются как `$name`, при этом имя может состоять только из букв, цифр, подчеркивания 
или апотсрофа, 

### Терминалы

Терминалы записываются в двойных кавычках `"`, правила те же, что и для нетерминалов.

### Константа EPS

Просто строка `EPS` 


### Описание выражений

Язык выражений определён следующим образом:

```
expr -> non-terminal
expr -> terminal
expr -> name
expr -> (expr)
expr -> [expr]
expr -> {expr}
expr -> EPS
expr -> expr expr

expr -> expr | expr
```

однозначная грамматика, задающая тот же язык:

```
term -> non-terminal
term -> terminal
term -> name
term -> (expr)
term -> [expr]
term -> {expr}
term -> EPS


seq -> term
seq -> seq term

expr -> seq
expr -> expr | seq
```

Т.е. выражениями являются все имена, нетерминалы, терминалы, выражения, записанные подряд выражения,
выражения через знак альтернативы.

## Описание разделов

### start
В разделе старт единственная строка, на ней записан единственный нетерминал (он определяется стартовым)
и точка с запятой


### names

Состоит из произвольного числа определений вида

`name := non-terminal;`, например 


```yaml
names:
  $if := "IF";
  $name = "Very very very long string";
```


### names

Состоит из произвольного числа определений вида

`non-terminal := expr;`, например

```yaml
rules:
  <S> := { <L> <S> $right } [<S>] | EPS;
  <L> := "(";
```

## Семантика

В выражениях

* квадратные скобки означают опциональность,
* фигурные - звезду Клини, 
* круглые - не влияют на семантику, нужны лишь для группировки, 
* EPS - пустую строку.

альтернатива менее приоритетна, чем конкатенация.


## Примеры

```yaml 
start: 
  <S>; 
names:
  $l := "(";
  $r := ")";
rules: 
  <S> := $l <S> $r <S> | EPS;   
```

```yaml 
start: 
  <Num>; 
names:
rules:
  <Digit> := "0"|"1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9";
  <Nat> := <Digit> {<Digit>};
  <Num> := ["+" | "-"] <Nat> ["."[<Nat>]] [("e" | "E") ["+" | "-"] <Nat>];
```



